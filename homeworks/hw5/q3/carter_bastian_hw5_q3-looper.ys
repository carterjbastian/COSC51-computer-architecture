# Hw5 Question 3 for CS51
# Carter Bastian, 4/30/2015

.pos 0
        # Set up Stack
        irmovl stack, %esp

        # Put in values to make sure state-storage works
        irmovl $3, %ecx
        irmovl $3, %edx
        irmovl $3, %ebx
        irmovl $3, %ebp
        irmovl $3, %esi
        irmovl $3, %edi

        # Test with wrapping
        irmovl $0xff000000, %eax
        call SHIFT4
        # %eax should be $0xf000000f

        # Test without wrapping
        irmovl $0x000ff000, %eax
        call SHIFT4
        # %eax should be $0x00ff0000        

        halt


        # subroutine: %eax <- %eax shifted 1
SHIFT1:
        # We use three registers, so we save old values here
        pushl %ecx
        pushl %edx
        pushl %ebx
        
        rrmovl %eax, %ecx # We're going to double %eax to shift it
        addl %ecx, %eax # %eax now contains %eax * 2 (%ecx contains orig)

        # Set up our increments
        irmovl $0, %edx # %edx contains 0
        irmovl $1, %ebx # %ebx contains 1

        subl %edx, %ecx  # Compare 0 with our orriginal value
        # If original value is Less than 0, our increment is 1
        cmovl %ebx, %edx

        addl %edx, %eax # "Wrap arround" if necessary

        # Restore orginal state
        popl %ebx
        popl %edx
        popl %ecx

        # Return
        ret

        #Subroutine <- %eax <- %eax shifted 4 and wrapped as necessary

SHIFT4:
        # We use 4 register values so we save old state here 
        # Callee-saving
        pushl %ecx
        pushl %edx
        pushl %ebx
        pushl %ebp

        # Assign our registers to the value we need
        irmovl $0, %ecx # Holds our index
        irmovl $1, %edx # Holds our constant increment value
        irmovl $4, %ebx # Number of times to increment
        rrmovl %ecx, %ebp # A copy of our index for comparison

        # Here's our fun little loop
LOOPSTART:
        subl %ebx, %ebp
        jge LOOPEND   # Break out of loop if done   
        call SHIFT1
        addl %edx, %ecx  # Increment our index
        rrmovl %ecx, %ebp # Restore our copy for comparison
        jmp LOOPSTART #Go back to the start

LOOPEND:
        # Restore original state
        # Callee-saving
        popl %ebp
        popl %ebx
        popl %edx
        popl %ecx

        # return once finished
        ret

.pos 0x500
stack:
        .long 0xFFFFFFFF # Top of the Empty Stack

